Include: ~Classes\BaseGrid~;

VOID ImportImagesSetupsGrid::ImportImagesSetupsGrid()					// CONSTRUCTOR
{
	TableName = ~ImportImagesSetups~;
	DocumentQuery = ~Select * from <TableName> Order By Setup~;
	Title = ~Import Images Setups~;
	WindowWidth = 75; WindowHeight = 30;
	HelpFile = ~KPISHelpFile~;
	HelpPage = ~ImportImagesSetup.htm~;
}

STR ImportImagesSetupsGrid::GetKey( &Item )					// PRIMARY KEY FOR THE TABLE
{
	return ~Setup = '<$con.Item[Setup]>'~;
}


NUMERIC ImportImagesSetupsGrid::SaveAllChanges()
{
	if (BaseGrid::SaveAllChanges())
	{
		RefreshListView();
	}

	return TRUE;
}

NUMERIC ImportImagesSetupsGrid::ProcessTextDrop( &Item )
{
	// <$con.Item[ID]>
	return TRUE;
}


NUMERIC ImportImagesSetupsGrid::OnDoubleClick()
{
	if ( !DataView( GetSelected,SelectedItems ) )	{ return Warning( ~There were no selected items~ ); }

	while ( SelectedItems( GetNext,Item ) )
	{
		Open( Any,~<$con.Item[Setup]>~ );
	}

	return TRUE;
}


VOID ImportImagesSetupsGrid::Init( theTitle )
{
	Title = ~<theTitle>~ == ~~ ? ~Import Images Setups~  : ~<theTitle>~;

	BaseGrid::Init();
}



NUMERIC Init(theTitle)
{
	if (Defined(Grid))
	{
		Grid.ReOpen();
		return TRUE;
	}

	Class( ImportImagesSetupsGrid,Extends,BaseGrid );

	ImportImagesSetupsGrid( New, Grid );

	Grid.DefineWindow();
	Grid.Init(~<theTitle>~);

	return Global( Grid );
}


NUMERIC ImportImagesSetupsGrid::DefineStdGridButtons( Subject )
{
	xPosition = $hGap;

	if ( MethodDefined( DeleteRecord ) )
	{
		Define: DeleteButton Using DDWinCtrlDef AsSymbol DeleteButton
			Fields:	GENCONNAME		This
					DACTIONSTEXT	ACTION EXEC ( <THIS>.DeleteRecord(TRUE) )
					POSITION		$xPosition, $vGap $StdButtonWidth $ButtonHeight
					TEXT			Translate( ~&Delete~ )
					TOOLTIP			Translate( ~Delete the selected <Subject>s~ )
					TYPENAME		BUTTON ;

		xPosition += $StdButtonWidth + $hGap;
	}

	if ( MethodDefined( RefreshListView ) )
	{
		Define: RefreshButton Using DDWinCtrlDef AsSymbol RefreshButton
			Fields:	GENCONNAME		This
					DACTIONSTEXT	ACTION EXEC ( <THIS>.RefreshListView() )
					POSITION		$xPosition, $vGap $StdButtonWidth $ButtonHeight
					TEXT			Translate( ~&Refresh~ )
					TOOLTIP			Translate( ~Refresh the list of <Subject>s~ )
					TYPENAME		BUTTON ;

			xPosition += $StdButtonWidth + $hGap;
	}

	return $xPosition;
}


VOID ImportImagesSetupsGrid::AddStdMenuItems()
{
	AddMenuItem( TRUE,	FALSE,Translate(UIMenuText,MenuDelete),ACTION EXEC ( <THIS>.DeleteRecord() ) );
	AddMenuItem( TRUE,	FALSE,Translate(UIMenuText,MenuRefresh),ACTION EXEC ( <THIS>.RefreshListView() ) );
}
