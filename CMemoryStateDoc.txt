NUMERIC DefineDialog()
{
	if ( !DictionaryExists( DDCMemoryStateDialog ) )
	{
        Dictionary: DDCMemoryStateDialog 
            Fields: Name	Field1		type text
 	}

	Define: DlgData Using DDCMemoryStateDialog;

	Left = 100;	Top = 100;	DialogWidth = 300; DialogHeight = 300;

	vGap = 5; hGap = 5; ButtonWidth = 90; ButtonHeight = 20;

	Define: aDialog Using DDWinDef
		Fields:	POSITION				100,100,$DialogWidth,$DialogHeight 
				TITLE					~CMemoryState Control~
				TYPENAME				~Dialog~ 
				STATUSBAR				~1 100~
				OACTIONSTEXT			ACTION EXEC ( DoCancelButton() )
				HACTIONSTEXT			ACTION SHOWHELPPAGE ( KPISHelpFile "DDCMemoryStateDialog.htm" ) 

		Contains: INLISTORDER
		{
			Define: CheckPointButton Using DDWinCtrlDef AsSymbol CheckPointButton 
				Fields:	GENCONNAME		THIS
						DACTIONSTEXT	ACTION EXEC ( CheckpointButton() )
						POSITION		($DialogWidth/2) - ($ButtonWidth) - $hGap,50, $ButtonWidth, $ButtonHeight
						TEXT			Translate( ~Checkpoint~ )
						TOOLTIP		Translate( ~Take a memory checkpoint~ )
						TYPENAME		BUTTON

			Define: DiffButton Using DDWinCtrlDef AsSymbol DiffButton
				Fields:	GENCONNAME		THIS
						DACTIONSTEXT	ACTION EXEC ( DifferenceButton() )
						POSITION		CheckPointButton(GetRight,Position) + $hGap,CheckPointButton(GetTop,Position),
									$ButtonWidth, $ButtonHeight
						TEXT			Translate( ~Dump Diff~ )
						TOOLTIP			Translate( ~Dump the memory used since the last checkpoint to the DevStudio log~ )
						TYPENAME		BUTTON
		}


	DialogHeight = aDialog(GetMaxBottom,Position) + $vGap + 30;

	aDialog( Set,Position,~<Left> <Top> <DialogWidth> <DialogHeight>~ );

	GenDlg( dlg,Definition,aDialog,Data,DlgData,Document,$Doc.This,New,Temporary );
	Global( dlg,DlgData );

	return TRUE;
}

NUMERIC CheckpointButton()
{
	if ( !Debug( IsDebugVersion ) )
	{
		return Warning( ~You must be running the Debug version of <$App.Title>~ );
	}

	dlg( SetStatus,0,~Memory checkpoint taken~ );

	Debug( MemoryCheckPoint );

	return TRUE;
}

NUMERIC DifferenceButton()
{
	if ( !Debug( IsDebugVersion ) )
	{
		return Warning( ~You must be running the Debug version of <$App.Title>~ );
	}

	Debug( DumpMemoryUsed );

	return dlg( SetStatus,0,~Memory difference dumped~ );
}

VOID DoCancelButton()
{
	dlg( End );
	Exit();
}

DefineDialog();
