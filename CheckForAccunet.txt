



NUMERIC IsAccunetInstalled( ShowUser )
{
	Problems = ~~;

	if ( !Registry( Reg1,OpenKey,~HKEY_LOCAL_MACHINE~,~Software\Bremson, Inc.\EAccuInt\100b~ ) )
	{
		Problems = Translate( ~InstallEAccuInt.exe has not been run.~ );

		if ( !<ShowUser> )	// IF NOT RUNNING INTERACTIVELY, DON'T BOTHER TO DO THE REST OF THE CHECKS
		{
			Accunet( accu,SetInstalled,0 );
			Log( Session,~<Problems>~ );
			return FALSE;
		}
	}

	if ( !Registry( Reg,OpenKey,~HKEY_LOCAL_MACHINE~,~Software\Bremson\100~ ) )
	{
		Problems = ~<Problems>\n~ + Translate( ~The Accunet LICENSEDRIVE key is not in the registry~ );	
	}
	else
	{
		a = Reg( QueryValue,LICENSEDRIVE,BasePath );
	}

	MakeTildeSafe( BasePath );

	if ( IsNULL( ~<Problems>~ ) && !IsNULL( ~<BasePath>~ ) )
	{
		BasePath = ~<BasePath>Common~;

		if ( IsNULL( String( StringInString,Upper(~W:\Common~),Upper(~<$Env.Path.QuoteSafe>~) ) ) )
		{
			Problems = ~<Problems>\n~ + Translate( ~Missing W:\Common in the System Path in <$Env.Path.QuoteSafe>. Did you reboot after installing?~ );
		}

		if ( !Files( FileExists,~<BasePath>\Eaccuint.dll~ ) )
		{
			Problems =  ~<Problems>\n~ + Translate( ~Can't find <BasePath>\Eaccuint.dll.  Is W: mapped to the Accunet System?~ );
		}

		if ( !Files( FileExists,~<BasePath>\Accuint.dll~ ) )
		{
			Problems =  ~<Problems>\n~ + Translate( ~Can't find  <BasePath>\Accuint.dll.  Is W: mapped to the Accunet System?~ );
		}

		if ( !Files( FileExists,~<BasePath>\AccunetConfig.dll~ ) )
		{
			Problems =  ~<Problems>\n~ + Translate( ~Can't find  <BasePath>\AccunetConfig.dll.  Is W: mapped to the Accunet System?~ );
		}
	}

	if ( !IsNull( ~<Problems>~ ) )
	{
		Problems = Translate( ~The interface to Accunet is not installed or set up correctly:\n\n~ ) + ~<Problems>~;

		Log( Session,~<Problems>~ );

		if ( <ShowUser> )
		{
			Warning( ~<Problems>~ + Translate( ~\n\nThe installer is InstallEaccuInt.exe in the Accunet folder of DP2.\nBe sure to read the README file after the install as there are manual steps you must perform.~ ) );
		}

		Accunet( accu,SetInstalled,0 );

		return FALSE;
	}

	Accunet( accu,SetInstalled,1 );	// INDICATE THAT ACCUNET IS INSTALLED

	if ( <ShowUser> )
	{
		IsAvailable =  accu( Reset ) ? ~Accunet is responding.~ : ~Accunet is not responding.~;

		return Warning( ~Accunet is installed.  <IsAvailable>\n\nIf you have an Accunet NT production system and you don't\nsee the Accunet programs in the task bar,\ntry reloading the tasks and macros from the Edit menu.~ );
	}

	Log( Session,~Accunet is installed.~ );

	return TRUE;
}
