IncludeClass( BaseMenu );
IncludeClass( BaseGrid );

VOID DSbaImages::DSbaImages( )					// CONSTRUCTOR
{
	TableName = ~DSbaImages~;
	Title = Translate( ~Auto Balance (Digital SBA) Images Data Table~ );
	DocumentQuery = ~Select * from <TableName> Order By OrderID,Roll,Frame ~;
	WindowTop = 0;
	WindowWidth = 90;
	WindowHeight = 25;
	HelpPage = ~AutoBalanceImages.htm~;
}

STR DSbaImages::GetKey( &Item )	
{ 
	// PRIMARY KEY FOR THE TABLE
		return ~OrderID = '<$con.Item[OrderID]>' AND Roll = '<$con.Item[Roll]>' AND Frame = '<$con.Item[Frame]>'~; 
}


NUMERIC DSbaImages::ChangeRecordAudits( &Changed )	// CALLED BEFORE ADDING OR CHANGING A RECORD
{
	if ( IsNULL(~<$Con.Changed[OrderID]>~) )
		{ return Warning( Translate( ~Unable to save pending changes.  Order cannot be blank.~ ) ); }

	if ( IsNull(~<$Con.Changed[Roll]>~) )
		{ return Warning( Translate( ~Unable to save pending changes.  Roll cannot be blank.~ ) ); }

	if ( IsNull(~<$Con.Changed[Frame]>~) )
		{ return Warning( Translate( ~Unable to save pending changes.  Frame cannot be blank.~ ) ); }

	return TRUE;
}

VOID DSbaImages::AddMenuItems()					// ADD ADDITIONAL RIGHT CLICK MENU ITEMS
{
	AddMenuItem( TRUE,FALSE,Translate(~Export Data~),ACTION EXEC ( <THIS>.ExportSelectedToFile() ) );

}

NUMERIC DSbaImages::DefineWindow()
{
	if ( !GenCon( TableDefinition,GetFrom,<TableName>,~[DATABASE <WhichTableDefinitions>]~ ) )
		{ return Warning( ~The <TableName> Table is not properly defined~ ); }

	Private( TableDefinition );

	Define: DataWindow Using DDWinDef AsSymbol DataWindow Private
		Fields:	Title					Translate(~<Title>~)
				Position				<WindowLeft> <WindowTop> <WindowWidth> <WindowHeight>
				IsPositionPercent		T
				TypeName				SplitterWnd
				SplitterRows			~<SplitterRows>~
				SplitterColumns			~<SplitterCols>~
				EnableStatusBar			<WantStatusBar> ? T : F ; 

				WindowDefinition();			// OVERRIDE to specify other DDWinDef parameters

		Contains:	INLISTORDER
		{
			Define: ControlView Using DDViewDef AsSymbol ControlView Private
				Fields:	ViewType				FormView
						KeyboardActionsText		ACTION Exec ( <THIS>.OnKeydown() ) 
						DropActionsText			ACTION Exec ( <THIS>.HandleDrop() )
						HActionsText			ACTION SHOWHELPPAGE ( <HelpFile> "<HelpPage>" );

						ControlViewDefinition();

				Contains:
				{
					position = AddControls( 0 );
				}

			Define: DataView Using DDViewDef AsSymbol DataView	Private
				Fields:	ViewType				~ListView~
						HActionsText			ACTION SHOWHELPPAGE ( <HelpFile> "<HelpPage>" )
						GridControlTypes		TableDefinition(GetReferenceTo,Fields)
						ChangeRecordActionsText ACTION Exec ( <THIS>.ChangeRecord() )
						DeleteRecordActionsText ACTION Exec ( <THIS>.DeleteRecord() )
						RClickUpActionsText		ACTION Exec ( <THIS>.RightClickInListView() )
						ComboBoxActionsText		ACTION Call ( <THIS>.OnComboBox() )
						DoubleClickActionsText	ACTION Exec ( <THIS>.DoubleClick() )
						ButtonClickActionsText	ACTION Exec ( <THIS>.OnButtonClick() )
						DropActionsText			ACTION Exec ( <THIS>.HandleDrop() )
						DragAcceptFiles			<DragAcceptFiles>
						CloseActionsText		ACTION Call ( <THIS>.OnCloseDataView() ) ;

						DataViewDefinition();
		}

	return TRUE;
}


NUMERIC DSbaImages::AddControls( position )
{
	Define: ExportDataButton Using DDWinCtrlDef AsSymbol ExportDataButton
		Fields:	GENCONNAME		This
				DACTIONSTEXT	ACTION EXEC ( <THIS>.ExportSelectedToFile() )
				POSITION		$position + $hGap, $vGap 60 $ButtonHeight
				TEXT			Translate( ~Export~ )
				TOOLTIP			Translate( ~Export data to a text file~ )
				TYPENAME		BUTTON

	Define: DSbaPrefsDataButton Using DDWinCtrlDef AsSymbol DSbaPrefsDataButton Private
		Fields:	TYPENAME		BUTTON
				DACTIONSTEXT	ACTION EXEC ( <THIS>.OnDSbaPrefsDataButton() )
				POSITION		ExportDataButton( GetRight,Position ) + $hGap, $vGap 170 $ButtonHeight
				TEXT			Translate( ~View Auto Bal Preferences Data~ )
				TOOLTIP			Translate( ~View the Auto Balance Preferences tracking data.~ );


	return DSbaPrefsDataButton( GetRight,Position );
}

NUMERIC DSbaImages::ProcessTextDrop( &Item )	{ return TRUE; }



STR DSbaImages::OnButtonClickTitle(Row,Col)
{
	if ( IsColumnName(<Col>,~Order~ ) )				{ return Translate(~Order ID for the image.~); }
	if ( IsColumnName(<Col>,~Roll~ ) )				{ return Translate(~Roll for the image..~); }
	if ( IsColumnName(<Col>,~Frame~ ) )				{ return Translate(~Frame for the image.~); }

	return BaseGrid::OnButtonClickTitle(<Row>,<Col>);
}


NUMERIC DSbaImages::OnDSbaPrefsDataButton()
{
	Actions = ACTION Doc ( DSbaPrefsDataDoc OpenOnce DSbaPrefsDataDoc "Init();" );
	PerformActions( Actions );

	return TRUE;
}


NUMERIC Init( OrderID,Roll,criteriaList )
{
	newDocQuery = ~~;
	if ( DefinedAndNotNULL( OrderID ) )
	{
		sort =  ~ Order By OrderID,Roll,Frame ~;
		newDocQuery = ~Select * from DSbaImages Where OrderID = '<OrderID>'~;
		if ( DefinedAndNotNull( Roll ) )
		{
			newDocQuery = ~<newDocQuery> AND Roll = '<Roll>' ~;
		}

		if ( DefinedAndNotNULL( criteriaList ) )
		{
			newDocQuery = ~<newDocQuery> AND <criteriaList>~;
		}

		newDocQuery = ~<newDocQuery><Sort>~;
	}
	else
	{
		newDocQuery = ~Select * from <TableName> Order By OrderID,Roll,Frame ~;
	}

	if ( Defined(DSbaImagesGrid) )
	{
		if ( ~<newDocQuery>~ != ~~ )
		{
			DSbaImagesGrid.Set( DocumentQuery,~<newDocQuery>~ );
		}

		DSbaImagesGrid.ReOpen();
		DSbaImagesGrid.RefreshListView();
		return TRUE;
	}

	Class( DSbaImages,Extends,BaseGrid );

	DSbaImages( New,DSbaImagesGrid );

	DSbaImagesGrid.DefineWindow();

	if ( ~<newDocQuery>~ != ~~ )
	{
		DSbaImagesGrid.Set( DocumentQuery,~<newDocQuery>~ );
	}

	DSbaImagesGrid.Init();
	return Global( DSbaImagesGrid );
}
