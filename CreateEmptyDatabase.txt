

NUMERIC CreateIndexes( &TableDefinition )
{
	if ( GenCon( IndexDefinitions,Is,TableDefinition(GetNumeric,Indexes) ) )
	{ 
		while ( IndexDefinitions( GetNext,IndexDefinition ) )
		{
			if ( !db( CMD,~<$con.IndexDefinition[Text]>~ ) )
			{
				db( GetErrorDescription,theErr );
				return Warning( ~Error: <theErr> From: [$con.IndexDefinition[Text]>]~ );
			}
		}
	}

	return TRUE;
}



NUMERIC CreateTable( &TableDefinition )
{
	TableName = ~<$con.TableDefinition[TableName]>~;

	if ( !db( CMD,~DROP TABLE <TableName>~ ) )
	{
		db( GetErrorNumber,theErrNo );
		db( GetErrorDescription,theErr );
		Log( Session,~Error <theErrNo>, Could not drop the TABLE <TableName> [<theErr>]~ );
		MessageBox( ~Could not drop the TABLE <TableName> [<theErr>]~ );
	}

	TableDefinition( CreateTableDefinition,CreationSQL,db );

	if ( !db( CMD,~CREATE TABLE <TableName> (<CreationSQL>)~ ) )
	{
		db( GetErrorDescription,theErr );
		Error( ~Error: <theErr>~,~From: [<theSQL>]~ );
	}

	return TRUE;
}


VOID DropTable( theTable )
{
	if ( !db( CMD,~DROP TABLE <theTable>~ ) )
	{
		db( GetErrorNumber,theErrNo );
		db( GetErrorDescription,theErr );
		Log( Session,~Error <theErrNo>, Could not drop the TABLE <theTable> [<theErr>]~ );
		MessageBox( ~Could not drop the TABLE <theTable> [<theErr>]~ );
	}
}


Response = MessageBox( ~This will create a new empty database.  All data in the database will be destroyed.  Are you sure?~,YESNO );

if ( <Response> == YES )
{
	if ( !ado( db,CONNECT ) )
		{	Error( ~Could not open Database~ );  }

	if ( !GenCon( TableDefinitions,GetFrom,TableDefinitions,~[Database]~ ) )
		{ Error( ~<Source> Does Not Exist~ ); }

	tables = ~~;

	while ( TableDefinitions( GetNext,TableDefinition ) )
	{
		CreateTable( TableDefinition );
		CreateIndexes( TableDefinition );
		tables = ~<tables> <$con.TableDefinition[TableName]>~;
	}

	MessageBox( ~Added the following tables: <tables>~ );
}
