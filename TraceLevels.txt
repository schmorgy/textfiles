

VOID SetupTraceLevelsDialog( void )
{
	Dictionary: DDTraceLevels
		Fields: Name All		Type long	LowValue 0	HighValue 5
		Fields: Name Jobs		Type long	LowValue 0	HighValue 5	
		Fields: Name Messages	Type long	LowValue 0	HighValue 5	
		Fields: Name Rendering	Type long	LowValue 0	HighValue 5	
		Fields: Name CInterp	Type long	LowValue 0	HighValue 5	

	Define: TraceLevels Using DDTraceLevels;
	Hang: TraceLevels [GENCONS];

	y = 5;		// starting X position
	h = 20;		// height of each control

	HGap = 10;
	VOffset = 25;
	
	PromptX = 5;
	PromptWidth = 100;

	ValueX = $PromptX + $PromptWidth + $HGap;
	ValueWidth = 60;

	SliderX = $ValueX + $ValueWidth + $HGap;
	SliderWidth = 100;

	DialogWidth = $SliderX + $SliderWidth + $HGap;

	Define: ~TraceLevelsDialog~ Using ~DDWinDef~
		Fields:	~IACTIONSTEXT~	~[NULL]~
				~OACTIONSTEXT~	~[NULL]~
				~POSITION~		0, 0, $DialogWidth, 600
				~TITLE~			~Set Tracing Levels~
				~TYPENAME~		~Dialog~ 
				HACTIONSTEXT	ACTION SHOWHELPPAGE ( KPISHelpFile "TLeading.htm" );


		Contains: INLISTORDER
		{
			index = 0;
			
			Category = ~<$AttrName.DDTraceLevels[$index]>~;

			while ( ~<Category>~ != ~~ )
			{
				Define: ~<Category>Prompt~ Using ~DDWinCtrlDef~
					Fields:
						~GENCONFIELD~	~Level~
						~POSITION~		$PromptX $y $PromptWidth $h
						~TEXT~			~<Category>~
						~TYPENAME~		~StaticText~ ;

				Define: ~<Category>Field~ Using ~DDWinCtrlDef~
					Fields:
						~DACTIONSTEXT~		~[Save Update(<Category>Slider)]~
						~GENCONFIELD~		~<Category>~
						~GENCONNAME~		~TraceLevels~
						~IACTIONSTEXT~		~[Get]~
						~OACTIONSTEXT~		~[Save]~
						~POSITION~			$ValueX $y $ValueWidth $h
						~TEXT~				~~
						~TOOLTIP~			~Type the trace level for <Category>~
						~TYPENAME~			~EditText~
						HACTIONSTEXT		ACTION SHOWHELPPAGE ( KPISHelpFile "Trace<Category>EditField.html" ) ;

				Define: ~<Category>Slider~ Using ~DDWinCtrlDef~
					Fields:
						~DACTIONSTEXT~		~[Save Update(<Category>Field)]~
						~GENCONFIELD~		~<Category>~
						~GENCONNAME~		~TraceLevels~
						~IACTIONSTEXT~		~[Get]~
						~OACTIONSTEXT~		~[Save]~
						~POSITION~			$SliderX $y $SliderWidth $h
						~TOOLTIP~			~Adjust the level for <Category>~
						~TYPENAME~			~SLIDER~ 
						~HACTIONSTEXT~		ACTION SHOWHELPTEXTFROMFILE ( "<$App.Dir>help\LoggingAndTracing.txt" <Category>Slider ) ;

				y += $VOffset;	// Bump to the next row of controls
				++index;		// Bump to next attribute in the GenCon
				Category = ~<$AttrName.DDTraceLevels[$index]>~;
			}

			// Center the Close button in the bottom of the dialog

			ButtonWidth = 90;

			Define: ~AddCtrlButton~ Using ~DDWinCtrlDef~
				Fields:	~GENCONFIELD~	~Command~
						~GENCONNAME~	~This~
						~DACTIONSTEXT~	~[Close]~
						~OACTIONSTEXT~ 	~[PERFORMOACTIONS]~
						~POSITION~		($DialogWidth / 2) - ($ButtonWidth / 2) , $y, $ButtonWidth, $h
						~TEXT~			~Close~
						~TOOLTIP~		~Close this dialog~
						~TYPENAME~		~BUTTON~
						HACTIONSTEXT	ACTION SHOWHELPPAGE ( KPISHelpFile "AutoLevel.htm" ) ;

		}

	DialogHeight = $y +  $h + $VOffset ;
	TraceLevelsDialog( Set,Position,~0  0  <DialogWidth>  <DialogHeight>~ );	
	Hang: ~TraceLevelsDialog~ ~[UI Dialogs]~ ;
}

SetupTraceLevelsDialog();



