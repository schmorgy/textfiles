IncludeClass( BaseDialog );

Class( LostLicenseDlg,Extends,BaseDialog,
		Member,fReasonText,Str,~~,
		Member,fDetailsText,Str,~~ );

VOID LostLicenseDlg::LostLicenseDlg()
{
	LoadTranslationFile( Administration,LicenseManagementTrans,LicenseManagementTrans);	

	Left = 515;
	Title = TR(LicenseManagementTrans, LicenseTitle);
}

VOID LostLicenseDlg::SetDlgValues()
{
	LostLicenseValue = ~0~;
	AppShutdownValue = ~0~;
	if ( GenCon(LLI,GetFrom,LLI,~[GenCons]~) )
	{
		LostLicenseValue = ~<$con.LLI[RS]>~;
		AppShutdownValue = ~<$con.LLI[TL]>~;
	}

	LostLicenseReason = Numeric(~<LostLicenseValue>~);
	AppShutdownTimeLeft = Numeric(~<AppShutdownValue>~);

	ReasonText = ~~;
	if ( <LostLicenseReason> == 1 )
	{
		ReasonText = TR(LicenseManagementTrans,AcquireLicenseExpired);
		DetailsText = TR(LicenseManagementTrans,ExpiredLicenseDetails,~<$App.Title>~,<AppShutdownTimeLeft>);		
	}
	else
	{
		ReasonText = TR(LicenseManagementTrans,AcquireLicenseFailed);		
		DetailsText = TR(LicenseManagementTrans,InvalidLicenseDetails,~<$App.Title>~,<AppShutdownTimeLeft>);		
	}

	dlg(SetControlValue,ReasonText,~<ReasonText>~);
	dlg(SetControlValue,DetailsText,~<DetailsText>~);
}

VOID LostLicenseDlg::WindowDefinition()
{
	Fields:	NoSystemMenu	T
			IsCentered		T
}

NUMERIC LostLicenseDlg::DefineControls()
{
	Define: ReasonText Using DDWinCtrlDef AsSymbol ReasonText
		Fields:	TYPENAME			STATICTEXT
				GENCONNAME			THIS
				LeftJustified		TRUE
				Position			<hgap> <vGap>+20, 400, 50
				Text				~~;

	Define: DetailsText Using DDWinCtrlDef AsSymbol DetailsText
		Fields:	TYPENAME			STATICTEXT
				GENCONNAME			THIS
				LeftJustified		TRUE
				Position			<hgap>, ReasonText(GetBottom,Position) + $vGap, 400, 50
				Text				~~;

	DialogWidth = aDialog(GetMaxRight,Position) + $hGap * 2;
	DialogHeight = aDialog(GetMaxBottom,Position);

	Define: OKButton Using DDWinCtrlDef AsSymbol OKButton
		Fields:	GENCONNAME			THIS
				DACTIONSTEXT		ACTION EXEC ( <THIS>.OnOKButton() )
				POSITION			($DialogWidth/2) - ($ButtonWidth/2) - $hGap,$DialogHeight+$vGap * 2 + 3, $ButtonWidth, $ButtonHeight
				TEXT				TR(UIMessages,OK)
				TYPENAME			BUTTON
				ISTABSTOP			TRUE

	return TRUE;				
}

NUMERIC LostLicenseDlg::OnOKButton()
{
	DoCancelButton();
}

VOID Init()
{
	if ( !Defined( LostLicense ) )
	{
		LostLicenseDlg( New,LostLicense );
		LostLicense.DefineDialog();
		LostLicense.SetDlgValues();
		Global( LostLicense );
	}
	else
	{
		LostLicense.SetDlgValues();
		LostLicense.ReOpen();
	}

	return;
}
