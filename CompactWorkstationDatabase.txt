
NUMERIC CompactWorkstationDatabase()
{
	DataBasePath = ~<$[DATABASE/Connections].Local_DB_Connection[Path]>~;

	CompactToPath = ~<$str.$DataBasePath.WithoutExt>Temp.mdb~;

	if ( !Files( Delete,~<CompactToPath>~ ) )
	{
		Log( Session,Translate( ~Could not delete the temporary file <CompactToPath>\nthat the database is compacted to because of the following error:\n\n<$Func.GetLastErrorMsg()>~ ));
		return FALSE;
	}

	if ( !CompactJetDatabase( ~<DataBasePath>~,~<CompactToPath>~ ) )
	{
		GetLastDatabaseError( ~Jet~, Error );
		if ( ( <Error> == 3045 ) || ( <Error> == 3356 ) )
		{
			Log( Session,Translate( UIMessages,FailedToGetExclusiveDBAccess,~<DataBasePath>~ ) );
			//return FALSE;

			// Could be another instance of the app running.  Lets don't assume this is a problem!
			Log( Session,~<Msg>~ );
			return TRUE;
		}
		else
		{
			Log( Session,Translate( ~Could not compact\n\n<DataBasePath>\nto\n<CompactToPath>~ ) );
			return FALSE;
		}
	}

	if ( !Files( FileExists,~<CompactToPath>~ ) )
	{
		Log( Session, Translate( ~The compact was successful but <CompactToPath> does not exist.~ ) );
		return FALSE;
	}

	if ( !Files( Delete,~<DataBasePath>~ ) )
	{
		Log( Session,Translate( ~Could not delete the original database file <DataBasePath>\nbecause of the following error:\n\n<$Func.GetLastErrorMsg()>~));
		return FALSE;
	}

	if ( !CompactJetDatabase( ~<CompactToPath>~,~<DataBasePath>~ ) )
	{
		Log( Session,Translate( ~Could not copy\n\n<CompactToPath>\nto\n<DataBasePath>~ ));
		return FALSE;
	}

	if ( !Files( Delete,~<CompactToPath>~ ) )
	{
		Log( Session,Translate( ~Could not delete <CompactToPath>\nbecause of the following error:\n\n<$Func.GetLastErrorMsg()>~) );
		return FALSE;
	}

	Log( Session,~Compacted <DataBasePath>~ );

	return TRUE;
}

CompactWorkstationDatabase();